select distinct ?iri where
{
	{
	    ?S ?iri ?O .
        filter(?iri != rdf:type)
        filter(?iri != owl:sameAs)
        filter(?iri != skos:prefLabel)
        filter(?iri != skos:altLabel)
        filter(?iri != skos:closeMatch)
        filter(?iri != cito:isDiscussedBy)
	}
    union
    {
        ?S rdf:type ?iri .
        filter(?iri != sio:SIO_010000)
    }
    union
    {
        ?S owl:sameAs ?iri .
        filter(replace(str(?S), 'http://rdf.ncbi.nlm.nih.gov/pubchem/taxonomy/TAXID', '') != replace(str(?iri), 'https://www.ncbi.nlm.nih.gov/taxonomy/', ''))
        filter(replace(str(?S), 'http://rdf.ncbi.nlm.nih.gov/pubchem/taxonomy/TAXID', '') != replace(str(?iri), 'https://identifiers.org/taxonomy:', ''))
    }
    union
    {
        ?S skos:closeMatch ?iri .
        filter(!strstarts(str(?iri), 'http://purl.uniprot.org/taxonomy/'))
        filter(!strstarts(str(?iri), 'http://id.nlm.nih.gov/mesh/'))
        filter(!strstarts(str(?iri), 'https://www.catalogueoflife.org/data/taxon/'))
        filter(!strstarts(str(?iri), 'http://ncicb.nci.nih.gov/xml/owl/EVS/Thesaurus.owl#C'))
        filter(!strstarts(str(?iri), 'https://www.itis.gov/servlet/SingleRpt/SingleRpt?search_topic=TSN&search_value='))
        filter(!strstarts(str(?iri), 'https://identifiers.org/mesh:'))
        filter(!strstarts(str(?iri), 'https://identifiers.org/ncit:C'))
        filter(!strstarts(str(?iri), 'https://identifiers.org/col:'))
    }
    union
    {
        ?S skos:closeMatch ?iri .
        filter(strstarts(str(?iri), 'https://identifiers.org/mesh:'))
        bind (replace(str(?iri), 'https://identifiers.org/mesh:', '') as ?mesh)
        
        minus
        {
            ?S skos:closeMatch ?iri2 .
            filter(strstarts(str(?iri2), 'http://id.nlm.nih.gov/mesh/'))
            bind (replace(str(?iri2), 'http://id.nlm.nih.gov/mesh/', '') as ?mesh)
        }
    }
    union
    {
        ?S skos:closeMatch ?iri .
        filter(strstarts(str(?iri), 'https://identifiers.org/col:'))
        bind (replace(str(?iri), 'https://identifiers.org/col:', '') as ?mesh)
        
        minus
        {
            ?S skos:closeMatch ?iri2 .
            filter(strstarts(str(?iri2), 'https://www.catalogueoflife.org/data/taxon/'))
            bind (replace(str(?iri2), 'https://www.catalogueoflife.org/data/taxon/', '') as ?mesh)
        }
    }
}
