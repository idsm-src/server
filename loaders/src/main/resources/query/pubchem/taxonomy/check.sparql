select distinct ?iri where
{
	{
	    ?S ?iri ?O .
        filter(?iri != rdf:type)
        filter(?iri != dcterms:identifier)
        filter(?iri != skos:prefLabel)
        filter(?iri != skos:altLabel)
        filter(?iri != rdfs:seeAlso)
        filter(?iri != cito:isDiscussedBy)
	}
    union
    {
        ?S rdf:type ?iri .
        filter(?iri != sio:SIO_010000)
    }
    union
    {
        ?iri dcterms:identifier ?O .
        filter(replace(str(?iri), 'http://rdf.ncbi.nlm.nih.gov/pubchem/taxonomy/TAXID', '') != str(?O))
    }
    union
    {
        ?S rdfs:seeAlso ?iri .
        filter(!strstarts(str(?iri), 'http://purl.uniprot.org/taxonomy/'))
        filter(!strstarts(str(?iri), 'http://id.nlm.nih.gov/mesh/'))
        filter(!strstarts(str(?iri), 'http://ncicb.nci.nih.gov/xml/owl/EVS/Thesaurus.owl#C'))
        filter(!strstarts(str(?iri), 'https://www.itis.gov/servlet/SingleRpt/SingleRpt?search_topic=TSN&search_value='))
        filter(!strstarts(str(?iri), 'http://identifiers.org/mesh:'))
        filter(!strstarts(str(?iri), 'http://identifiers.org/ncit:C'))
        filter(!strstarts(str(?iri), 'http://identifiers.org/col:'))
        filter(!strstarts(str(?iri), 'http://identifiers.org/taxonomy:'))
        filter(!strstarts(str(?iri), 'http://rdf.glycoinfo.org/source/'))
        filter(!strstarts(str(?iri), 'https://glycosmos.org/organisms/show/'))
    }
    union
    {
        ?S rdfs:seeAlso ?iri .
        filter(strstarts(str(?iri), 'http://identifiers.org/mesh:'))
        bind (replace(str(?iri), 'http://identifiers.org/mesh:', '') as ?mesh)
        
        minus
        {
            ?S rdfs:seeAlso ?iri2 .
            filter(strstarts(str(?iri2), 'http://id.nlm.nih.gov/mesh/'))
            bind (replace(str(?iri2), 'http://id.nlm.nih.gov/mesh/', '') as ?mesh)
        }
    }
    union
    {
        ?S rdfs:seeAlso ?iri .
        filter(strstarts(str(?iri), 'http://identifiers.org/taxonomy:'))
        filter(replace(str(?S), 'http://rdf.ncbi.nlm.nih.gov/pubchem/taxonomy/TAXID', '') != replace(str(?iri), 'http://identifiers.org/taxonomy:', ''))
    }
}
