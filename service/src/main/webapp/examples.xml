<?xml version="1.0" encoding="UTF-8"?>
<examples>
  <example>
    <description><![CDATA[
      <div lang="en" class="exampleTitle">PubChemRDF Case 1:</div>
      <p lang="en" class="exampleDescription">
        What protein targets does donepezil (CHEBI_53289) inhibit with an IC<sub>50</sub> less than 10 \u00b5M?
      </p>
    ]]></description>
    <code><![CDATA[
SELECT DISTINCT ?protein
FROM pubchem:protein
FROM pubchem:measuregroup
FROM pubchem:endpoint
FROM pubchem:substance
WHERE
{
  ?sub rdf:type obo:CHEBI_53289;
     obo:RO_0000056 ?mg.
  ?mg obo:RO_0000057 ?protein;
    obo:OBI_0000299 ?ep.
  ?protein rdf:type sio:SIO_010043.
  ?ep rdf:type bao:BAO_0000190;
    obo:IAO_0000136 ?sub;
    sio:SIO_000300|sio:SIO_000738 ?value.

  filter (?value < 10)
}
    ]]></code>
  </example>

  <example>
    <description><![CDATA[
      <div lang="en" class="exampleTitle">PubChemRDF Case 2:</div>
      <p lang="en" class="exampleDescription">
        What pharmacological roles of SID46505803 are defined by ChEBI?
      </p>
    ]]></description>
    <code><![CDATA[
SELECT DISTINCT ?role
FROM pubchem:substance
FROM pubchem:compound
FROM ebi:chebi
WHERE
{
  substance:SID46505803 sio:CHEMINF_000477 ?comp.
  ?comp rdf:type ?chebi.
  ?chebi rdfs:subClassOf [
    a owl:Restriction;
    owl:onProperty obo:RO_0000087;
    owl:someValuesFrom ?role ].
}
    ]]></code>
  </example>

  <example>
    <description><![CDATA[
      <div lang="en" class="exampleTitle">PubChemRDF Case 3:</div>
      <p lang="en" class="exampleDescription">
        What compound have a pharmacological role of NSAID as defined by ChEBI and molecular weight less than 250 g/mol?
      </p>
    ]]></description>
    <code><![CDATA[
SELECT DISTINCT ?compound
FROM pubchem:compound
FROM ebi:chebi
FROM descriptor:compound
WHERE
{
  ?compound rdf:type ?chebi.
  ?chebi rdfs:subClassOf [
    a owl:Restriction;
    owl:onProperty obo:RO_0000087;
    owl:someValuesFrom obo:CHEBI_35475 ].
  ?compound sio:SIO_000008 ?MW.
  ?MW rdf:type sio:CHEMINF_000334.
  ?MW sio:SIO_000300 ?MWValue.

  filter (?MWValue < 250)
}
    ]]></code>
  </example>

  <example>
    <description><![CDATA[
      <div lang="en" class="exampleTitle">PubChemRDF Case 4:</div>
      <p lang="en" class="exampleDescription">
        What substances have a pharmacological role of NSAID as defined by ChEBI and the depositor-provided chemical vendor information?
      </p>
    ]]></description>
    <code><![CDATA[
SELECT DISTINCT ?substance ?source
FROM pubchem:substance
FROM pubchem:source
FROM ebi:chebi
WHERE
{
  ?substance dcterms:source ?source.
  ?source dcterms:subject concept:Chemical_Vendors.
  ?substance rdf:type ?chebi.
  ?chebi rdfs:subClassOf [
    a owl:Restriction;
    owl:onProperty obo:RO_0000087;
    owl:someValuesFrom obo:CHEBI_35475 ].
}
    ]]></code>
  </example>

  <example>
    <description><![CDATA[
      <div lang="en" class="exampleTitle">PubChemRDF Case 5:</div>
      <p lang="en" class="exampleDescription">
        What protein targets are inhibited by substances with an IC<sub>50</sub> less than 10 \u00b5M and have a pharmacological role of cholinesterase inhibitors as defined by ChEBI?
      </p>
    ]]></description>
    <code><![CDATA[
SELECT DISTINCT ?protein
FROM pubchem:substance
FROM pubchem:measuregroup
FROM pubchem:endpoint
FROM pubchem:protein
FROM ebi:chebi
WHERE
{
  ?chebi rdfs:subClassOf [
    a owl:Restriction;
    owl:onProperty obo:RO_0000087;
    owl:someValuesFrom obo:CHEBI_37733 ].
  ?sub rdf:type ?chebi;
    obo:RO_0000056 ?mg.
  ?mg obo:RO_0000057 ?protein;
    obo:OBI_0000299 ?ep.
  ?protein rdf:type sio:SIO_010043.
  ?ep rdf:type bao:BAO_0000190;
    obo:IAO_0000136 ?sub;
    sio:SIO_000300|sio:SIO_000738 ?value.

  filter (?value < 10)
}
    ]]></code>
  </example>

  <example>
    <description><![CDATA[
      <div lang="en" class="exampleTitle">PubChemRDF Case 6:</div>
      <p lang="en" class="exampleDescription">
        Which substances inhibit protein targets similar to ACCNP_542158 and have the function domain PSSMID188648?
      </p>
    ]]></description>
    <code><![CDATA[
SELECT DISTINCT ?substance ?protein ?endpoint
FROM pubchem:substance
FROM pubchem:measuregroup
FROM pubchem:endpoint
FROM pubchem:protein
FROM pubchem:conserveddomain
WHERE
{
  ?substance obo:RO_0000056 ?measuregroup.
  ?measuregroup obo:RO_0000057 ?protein.
  protein:ACCNP_542158 vocab:hasSimilarProtein ?protein.
  ?protein obo:RO_0002180 conserveddomain:PSSMID188648.
  ?measuregroup obo:OBI_0000299 ?endpoint.
  ?endpoint obo:IAO_0000136 ?substance.
  ?endpoint rdf:type bao:BAO_0000190.
}
    ]]></code>
  </example>

  <example>
    <description><![CDATA[
      <div lang="en" class="exampleTitle">PubChemRDF Case 7:</div>
      <p lang="en" class="exampleDescription">
        What protein targets are inhibited by substances with IC<sub>50</sub> less than 10 \u00b5M and have the same standardized chemical structure (CID3152)?
      </p>
    ]]></description>
    <code><![CDATA[
SELECT DISTINCT ?sub ?protein
FROM pubchem:protein
FROM pubchem:measuregroup
FROM pubchem:endpoint
FROM pubchem:substance
WHERE
{
  ?sub sio:CHEMINF_000477 compound:CID3152;
    obo:RO_0000056 ?mg.
  ?mg obo:RO_0000057 ?protein;
    obo:OBI_0000299 ?ep.
  ?protein rdf:type sio:SIO_010043.
  ?ep rdf:type bao:BAO_0000190;
    obo:IAO_0000136 ?sub;
    sio:SIO_000300|sio:SIO_000738 ?value.

  filter (?value < 10)
}
    ]]></code>
  </example>

  <example>
    <description><![CDATA[
      <div lang="en" class="exampleTitle">PubChemRDF Case 8:</div>
      <p lang="en" class="exampleDescription">
         What substances inhibit the proteins involved in the same biological pathway: prostaglandin biosynthetic process (GO:0001516), with an IC<sub>50</sub> less than 10 \u00b5M?
      </p>
    ]]></description>
    <code><![CDATA[
SELECT DISTINCT ?substance ?protein
FROM pubchem:substance
FROM pubchem:measuregroup
FROM pubchem:endpoint
FROM pubchem:protein
FROM pubchem:pathway
FROM pubchem:gene
WHERE
{
  ?substance obo:RO_0000056 ?measuregroup.
  ?measuregroup obo:RO_0000057 ?protein.
  ?protein rdf:type sio:SIO_010043.
  ?protein up:encodedBy ?gene .
  ?gene rdf:type sio:SIO_010035 .
  ?gene obo:RO_0000056 obo:GO_0001516 .
  ?measuregroup obo:OBI_0000299 ?endpoint.
  ?endpoint obo:IAO_0000136 ?substance.
  ?endpoint rdf:type bao:BAO_0000190.
  ?endpoint sio:SIO_000300|sio:SIO_000738 ?value.

  filter (?value < 10)
}
    ]]></code>
  </example>

  <example>
    <description><![CDATA[
      <div lang="en" class="exampleTitle">PubChemRDF Case 9:</div>
      <p lang="en" class="exampleDescription">
        What the pharmacological roles defined by ChEBI are for the substances that inhibit protein target ACCQ12809 with an IC<sub>50</sub> less than 10 \u00b5M?
      </p>
    ]]></description>
    <code><![CDATA[
SELECT DISTINCT ?role
FROM pubchem:measuregroup
FROM pubchem:endpoint
FROM pubchem:substance
FROM ebi:chebi
WHERE
{
  ?sub obo:RO_0000056 ?mg.
  ?mg obo:RO_0000057 protein:ACCQ12809;
    obo:OBI_0000299 ?ep.
  ?sub rdf:type ?chebi.
  ?chebi rdfs:subClassOf [
    a owl:Restriction;
    owl:onProperty obo:RO_0000087;
    owl:someValuesFrom ?role ].
  ?ep obo:IAO_0000136 ?sub;
    rdf:type bao:BAO_0000190;
    sio:SIO_000300|sio:SIO_000738 ?value.

  filter (?value < 10)
}
    ]]></code>
  </example>

  <example>
    <description><![CDATA[
      <div lang="en" class="exampleTitle">PubChemRDF Case 10:</div>
      <p lang="en" class="exampleDescription">
        Summarize the statistics about the total number of substances tested in the PubChem database against each protein target.
      </p>
    ]]></description>
    <code><![CDATA[
SELECT (count(?sub) as ?subcnt) ?protein
FROM pubchem:substance
FROM pubchem:measuregroup
FROM pubchem:endpoint
FROM pubchem:protein
WHERE
{
  ?sub obo:RO_0000056 ?mg.
  ?mg obo:RO_0000057 ?protein.
  ?protein rdf:type sio:SIO_010043.
  ?mg obo:OBI_0000299 ?ep.
  ?ep rdf:type bao:BAO_0000190;
    obo:IAO_0000136 ?sub.
}
GROUP BY ?protein
ORDER BY DESC(count(?sub))
    ]]></code>
  </example>

</examples>
